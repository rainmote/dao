(ns agent.plugins.openai
  "OpenAI-compatible streaming adapter, including DeepSeek tool calls."
  (:require [agent.cancellation :as cancellation]
            [agent.kernel :as kernel]
            [agent.streaming :as streaming]
            [babashka.http-client :as http]
            [cheshire.core :as json]
            [clojure.string :as str]))

(defn- endpoint [base-url]
  (str (str/replace base-url #"/+$" "") "/chat/completions"))

(defn- api-key [{:keys [api-key api-key-env]}]
  (or api-key
      (when api-key-env (System/getenv api-key-env))))

(defn- error-body [body]
  (let [text (if (string? body)
               body
               (try (slurp body) (catch Throwable _ (pr-str body))))]
    (subs text 0 (min 1200 (count text)))))

(defn- append-tool-call [calls chunk]
  (let [index (or (:index chunk) 0)
        current (get calls index {})
        function (:function chunk)]
    (assoc calls index
           (cond-> current
             (:id chunk) (assoc :id (:id chunk))
             (:type chunk) (assoc :type (:type chunk))
             (:name function)
             (update-in [:function :name] str (:name function))
             (:arguments function)
             (update-in [:function :arguments] str (:arguments function))))))

(defn- stream-response [ctx body cancel-token]
  (let [state (atom {:content [] :tool-calls {} :usage {}})]
    (streaming/consume-sse!
     body cancel-token
     (fn [event]
       (when-let [error (:error event)]
         (kernel/emit! ctx :llm/stream
                       {:type :response/error
                        :provider :openai-compatible
                        :error error})
         (throw (ex-info (or (:message error) "LLM stream failed")
                         {:provider :openai-compatible})))
       (swap! state
              (fn [current]
                (let [choice (first (:choices event))
                      delta (:delta choice)
                      content (:content delta)]
                  (when-not (str/blank? content)
                    (kernel/emit! ctx :llm/stream
                                  {:type :text/delta
                                   :provider :openai-compatible
                                   :delta content}))
                  (when-not (str/blank? (:reasoning_content delta))
                    (kernel/emit! ctx :llm/stream
                                  {:type :reasoning/delta
                                   :provider :openai-compatible
                                   :delta (:reasoning_content delta)}))
                  (cond-> current
                    (:model event) (assoc :model (:model event))
                    (:usage event) (assoc :usage (:usage event))
                    (:finish_reason choice)
                    (assoc :finish-reason (:finish_reason choice))
                    (not (str/blank? content))
                    (update :content conj content)
                    (seq (:tool_calls delta))
                    (update :tool-calls
                            #(reduce append-tool-call % (:tool_calls delta)))))))))
    (let [{:keys [content tool-calls finish-reason usage model]} @state
          calls (->> tool-calls (sort-by key) (mapv val))
          text (apply str content)]
      (kernel/emit! ctx :llm/stream
                    {:type :response/completed
                     :provider :openai-compatible})
      {:message (cond-> {:role "assistant"
                         :content (when-not (str/blank? text) text)}
                  (seq calls) (assoc :tool_calls calls))
       :finish-reason (or finish-reason
                          (if (seq calls) "tool_calls" "stop"))
       :usage usage
       :model model})))

(defn- json-response [ctx body]
  (let [parsed (json/parse-string body true)
        choice (first (:choices parsed))]
    (when-not (and choice (:message choice))
      (throw (ex-info "LLM response has no choice/message" {:body parsed})))
    (when-let [content (get-in choice [:message :content])]
      (when-not (str/blank? content)
        (kernel/emit! ctx :llm/stream
                      {:type :text/delta
                       :provider :openai-compatible
                       :delta content})))
    (kernel/emit! ctx :llm/stream
                  {:type :response/completed
                   :provider :openai-compatible})
    {:message (:message choice)
     :finish-reason (:finish_reason choice)
     :usage (:usage parsed)
     :model (:model parsed)}))

(defn- chat-content [content]
  (if-not (sequential? content)
    content
    (mapv (fn [block]
            (cond
              (contains? #{:text "text"} (:type block))
              {:type "text" :text (:text block)}

              (contains? #{:image "image"} (:type block))
              {:type "image_url"
               :image_url (cond-> {:url (or (:url block)
                                             (:image_url block))}
                            (:detail block) (assoc :detail (:detail block)))}

              :else (throw (ex-info "Unsupported chat content block"
                                    {:block block}))))
          content)))

(defn- normalize-message [message]
  (if (= "user" (:role message))
    (update message :content chat-content)
    message))

(defn- generate [ctx config request]
  (let [auth-id (or (:auth-id config) (:provider-id config)
                    :openai-compatible)
        key (if-let [store (kernel/service ctx :auth/store)]
              (:api-key ((:resolve! store) auth-id))
              (api-key config))]
    (when (str/blank? key)
      (throw (ex-info (str "Missing API key; set "
                           (or (:api-key-env config) ":api-key"))
                      {:provider :openai-compatible})))
    (let [messages (cond-> (mapv normalize-message (:messages request))
                     (not (str/blank? (:system-prompt request)))
                     (->> (into [{:role "system"
                                  :content (:system-prompt request)}])))
          use-stream? (not= false (:stream config))
          body (merge {:model (:model config)
                       :messages messages
                       :stream use-stream?}
                      (when (seq (:tools request))
                        {:tools (:tools request)
                         :tool_choice "auto"})
                      (:extra-body config)
                      (:options request))
          _ (cancellation/throw-if-cancelled! (:cancel-token request))
          response (http/post (endpoint (:base-url config))
                              {:headers {"authorization" (str "Bearer " key)
                                         "content-type" "application/json"}
                               :body (json/generate-string body)
                               :as (if use-stream? :stream :string)
                               :throw false
                               :timeout (or (:timeout-ms config) 120000)})]
      (when-not (<= 200 (:status response) 299)
        (throw (ex-info (str "LLM request failed with HTTP " (:status response))
                        {:status (:status response)
                         :body (error-body (:body response))})))
      (cancellation/throw-if-cancelled! (:cancel-token request))
      (if (and (string? (:body response))
               (str/starts-with? (str/trim (:body response)) "{"))
        (json-response ctx (:body response))
        (stream-response ctx (:body response) (:cancel-token request))))))

(def plugin
  {:id :llm/openai-compatible
   :description "OpenAI-compatible chat-completions adapter."
   :provides #{:llm/generate}
   :start
   (fn [ctx config]
     (when-not (and (string? (:base-url config))
                    (string? (:model config)))
       (throw (ex-info "OpenAI plugin requires :base-url and :model"
                       {:config (dissoc config :api-key)})))
     (when-let [store (kernel/service ctx :auth/store)]
       (kernel/track-effect!
        ctx
        ((:register! store)
         (or (:auth-id config) (:provider-id config) :openai-compatible)
         (fn [] {:api-key (api-key config)})
         {:provider :openai-compatible
          :source (if (:api-key config) :config :environment)})))
     (let [generator (partial generate ctx config)]
       (if-let [registry (kernel/service ctx :llm/registry)]
         (kernel/track-effect!
          ctx
          ((:register! registry)
           {:id (or (:provider-id config) :openai-compatible)
            :provider :openai-compatible
            :model (:model config)
            :context-window (:context-window config)
            :thinking-levels (:thinking-levels config)
            :input-modalities (or (:input-modalities config) #{:text})
            :generate generator}))
         (kernel/register-service! ctx :llm/generate generator)))
     nil)})
